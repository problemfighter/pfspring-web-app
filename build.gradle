plugins {
    id 'org.springframework.boot' version "$springboot" apply false
    id 'io.spring.dependency-management' version "$dependencyManagement" apply false
}


group = 'com.problemfighter.pfspring.webapp'
version = '1.0.0'


allprojects {

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/hmtmcse/maven/" }
        maven { url "https://dl.bintray.com/problemfighter/java/" }
        maven { url "https://repo.maven.apache.org/maven2/" }
    }

    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = '11'
    dependencies {}
}


Boolean isSpringBootProject(String name) {
    String projectRoot = rootProject.projectDir
    List projectRoots = [dependencySource, applicationPath]
    for (String projectRootName : projectRoots) {
        File rootPath = new File(projectRoot, projectRootName)
        List modules = ["module"]
        for (String moduleName : modules) {
            File subsectionDirectory = new File(rootPath, moduleName)
            if (subsectionDirectory.exists()) {
                if (subsectionDirectory.list().contains(name)) {
                    return true
                }

            }
        }
    }
    return false
}


subprojects { project ->

    if (isSpringBootProject(project.name)) {
        println("Loading SB Module: " + project.name)
        apply from: "${rootProject.projectDir}/${gradleAppConfig}/sbc-dependencies.gradle"
        bootJar { enabled = false }
        jar { enabled = true }
    }
}


//   Source Development Task
apply from: "${rootProject.projectDir}/${gradleAppConfig}/source-development.gradle"
task prepareSourceDevelopment {
    dependsOn resolveSourceDependencies
    doLast {
        println "Preparing Source Development"
    }
}