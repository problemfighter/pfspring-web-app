buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/hmtmcse/maven/" }
        maven { url "https://repo.maven.apache.org/maven2/" }
    }
    dependencies {
        classpath 'org.ajoberstar.grgit:grgit-core:3.1.1'
    }
}

def projectDependencies = [
        [
                "name"        : "None Spring Library",
                "destination" : "$dependencySource" + "library/",
                "dependencies": [
                        "pf-java-object-copier": "https://github.com/problemfighter/pf-java-object-copier.git",
                ]
        ],
        [
                "name"        : "Spring Boot Library",
                "destination" : "$dependencySource" + "module/",
                "dependencies": [
                        "pfspring-common"  : "https://github.com/problemfighter/pfspring-common.git",
                        "pfspring-rest-api": "https://github.com/problemfighter/pfspring-rest-api.git",
                ]
        ]
]


void cloneProject(List projectList) {
    String destinationPath = ""
    projectList.each { Map dependencyMap ->
        destinationPath = dependencyMap.destination
        println("Now Cloning ${dependencyMap.name}")
        dependencyMap.dependencies.each { name, url ->
            println("------------------------------------------------------------------------------------------")
            def destination = file(destinationPath + name)
            try {
                println("Cloning Project ${name}")
                org.ajoberstar.grgit.Grgit.clone(dir: destination, uri: url)
            } catch (Exception e) {
                println(e.getMessage())
            }
            println("------------------------------------------------------------------------------------------\n")
        }
    }
}

task resolveSourceDependencies {
    doLast {
        if (isSourceDevelopment.equals("true")) {
            cloneProject(projectDependencies)
        } else {
            println "Source Development not enabled, Please check the gradle.properties file."
        }
    }
}

def testProject = [
        [
                "name"        : "Spring Boot Test Library",
                "destination" : "$applicationPath" + "module/",
                "dependencies": [
                        "pfspring-web-test-module": "https://github.com/problemfighter/pfspring-web-test-module.git",
                ]
        ]
]

task cloneStudentSubProject {
    doLast {
        cloneProject(testProject)
    }
}


String testFeature = System.getProperty("test-feature")
if (testFeature && testFeature.equals("yes")) {
    println("------------- Cloning Test Project -------------")
    build.dependsOn cloneStudentSubProject
}

// For Heroku Build
task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean
stage.dependsOn(cloneStudentSubProject)